CP/M MACRO ASSEM 2.0	#001	mds cold start loader at 3000h

                	TITLE	'mds cold start loader at 3000h'
                ;
                ;	MDS-800 COLD START LOADER FOR CP/M 2.0
                ;
                ;	VERSION 2.0 AUGUST, 1979
                ;
 0000 =         FALSE	EQU	0
 FFFF =         TRUE	EQU	NOT FALSE
 0000 =         TESTING	EQU	FALSE	;IF TRUE, THEN GO TO MON80 ON ERRORS
                ;
                	IF	TESTING
                BIAS	EQU	03400H
                	ENDIF
                	IF	NOT TESTING
 0000 =         BIAS	EQU	0000H
                	ENDIF
 0000 =         CPMB	EQU	BIAS		;BASE OF DOS LOAD
 0806 =         BDOS	EQU	806H+BIAS	;ENTRY TO DOS FOR CALLS
 1880 =         BDOSE	EQU	1880H+BIAS	;END OF DOS LOAD
 1600 =         BOOT	EQU	1600H+BIAS	;COLD START ENTRY POINT
 1603 =         RBOOT	EQU	BOOT+3		;WARM START ENTRY POINT
                ;
 3000           	ORG	03000H	;LOADED DOWN FROM HARDWARE BOOT AT 3000H
                ;
 1880 =         BDOSL	EQU	BDOSE-CPMB
 0002 =         NTRKS	EQU	2	;NUMBER OF TRACKS TO READ
 0031 =         BDOSS	EQU	BDOSL/128	;NUMBER OF SECTORS IN DOS
 0019 =         BDOS0	EQU	25	;NUMBER OF BDOS SECTORS ON TRACK 0
 0018 =         BDOS1	EQU	BDOSS-BDOS0	;NUMBER OF SECTORS ON TRACK 1
                ;
 F800 =         MON80	EQU	0F800H	;INTEL MONITOR BASE
 FF0F =         RMON80	EQU	0FF0FH	;RESTART LOCATION FOR MON80
 0078 =         BASE	EQU	078H	;'BASE' USED BY CONTROLLER
 0079 =         RTYPE	EQU	BASE+1	;RESULT TYPE
 007B =         RBYTE	EQU	BASE+3	;RESULT BYTE
 007F =         RESET	EQU	BASE+7	;RESET CONTROLLER
                ;
 0078 =         DSTAT	EQU	BASE	;DISK STATUS PORT
 0079 =         ILOW	EQU	BASE+1	;LOW IOPB ADDRESS
 007A =         IHIGH	EQU	BASE+2	;HIGH IOPB ADDRESS
 00FF =         BSW	EQU	0FFH	;BOOT SWITCH
 0003 =         RECAL	EQU	3H	;RECALIBRATE SELECTED DRIVE
 0004 =         READF	EQU	4H	;DISK READ FUNCTION
 0100 =         STACK	EQU	100H	;USE END OF BOOT FOR STACK
                ;
                RSTART:
 3000 310001    	LXI	SP,STACK;IN CASE OF CALL TO MON80
                ;	CLEAR DISK STATUS
 3003 DB79      	IN	RTYPE
 3005 DB7B      	IN	RBYTE
                ;	CHECK IF BOOT SWITCH IS OFF
                COLDSTART:
 3007 DBFF      	IN	BSW
 3009 E602      	ANI	02H	;SWITCH ON?
 300B C20730    	JNZ	COLDSTART
                ;	CLEAR THE CONTROLLER
CP/M MACRO ASSEM 2.0	#002	mds cold start loader at 3000h

 300E D37F      	OUT	RESET	;LOGIC CLEARED
                ;
                ;
 3010 0602      	MVI	B,NTRKS	;NUMBER OF TRACKS TO READ
 3012 214230    	LXI	H,IOPB0
                ;
                START:
                ;
                ;	READ FIRST/NEXT TRACK INTO CPMB
 3015 7D        	MOV	A,L
 3016 D379      	OUT	ILOW
 3018 7C        	MOV	A,H
 3019 D37A      	OUT	IHIGH
 301B DB78      WAIT0:	IN	DSTAT
 301D E604      	ANI	4
 301F CA1B30    	JZ	WAIT0
                ;
                ;	CHECK DISK STATUS
 3022 DB79      	IN	RTYPE
 3024 E603      	ANI	11B
 3026 FE02      	CPI	2
                ;
                	IF	TESTING
                	CNC	RMON80	;GO TO MONITOR IF 11 OR 10
                	ENDIF
                	IF	NOT TESTING
 3028 D20030    	JNC	RSTART	;RETRY THE LOAD
                	ENDIF
                ;
 302B DB7B      	IN	RBYTE	;I/O COMPLETE, CHECK STATUS
                ;	IF NOT READY, THEN GO TO MON80
 302D 17        	RAL
 302E DC0FFF    	CC	RMON80	;NOT READY BIT SET
 3031 1F        	RAR		;RESTORE
 3032 E61E      	ANI	11110B	;OVERRUN/ADDR ERR/SEEK/CRC/XXXX
                ;
                	IF	TESTING
                	CNZ	RMON80	;GO TO MONITOR
                	ENDIF
                	IF	NOT TESTING
 3034 C20030    	JNZ	RSTART	;RETRY THE LOAD
                	ENDIF
                ;
                ;
 3037 110700    	LXI	D,IOPBL	;LENGTH OF IOPB
 303A 19        	DAD	D	;ADDRESSING NEXT IOPB
 303B 05        	DCR	B	;COUNT DOWN TRACKS
 303C C21530    	JNZ	START
                ;
                ;
                ;	JMP TO BOOT TO PRINT INITIAL MESSAGE, AND SET UP JMPS
 303F C30016    	JMP	BOOT
                ;
                ;	PARAMETER BLOCKS
 3042 80        IOPB0:	DB	80H	;IOCW, NO UPDATE
 3043 04        	DB	READF	;READ FUNCTION
CP/M MACRO ASSEM 2.0	#003	mds cold start loader at 3000h

 3044 19        	DB	BDOS0	;# SECTORS TO READ ON TRACK 0
 3045 00        	DB	0	;TRACK 0
 3046 02        	DB	2	;START WITH SECTOR 2 ON TRACK 0
 3047 0000      	DW	CPMB	;START AT BASE OF BDOS
 0007 =         IOPBL	EQU	$-IOPB0
                ;
 3049 80        IOPB1:	DB	80H
 304A 04        	DB	READF
 304B 18        	DB	BDOS1	;SECTORS TO READ ON TRACK 1
 304C 01        	DB	1	;TRACK 1
 304D 01        	DB	1	;SECTOR 1
 304E 800C      	DW	CPMB+BDOS0*128	;BASE OF SECOND READ
                ;
 3050           	END
